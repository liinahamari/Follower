buildscript {
    ext.kotlin_version = "1.6.10"
    ext.nav_version = '2.4.1'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "io.objectbox:objectbox-gradle-plugin:2.9.1"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

project.ext {
    minSdkVersion = 26
    compileSdkVersion = 31
    targetSdkVersion = 31
    versionCode = 1
    versionName = '1.0'
    buildToolsVersion = '30.0.3'
}

def setupAndroidDefaults(android) {
    android.compileSdkVersion rootProject.ext.compileSdkVersion
    android.buildToolsVersion rootProject.ext.buildToolsVersion

    android.defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "int", "VERSION_CODE", "${rootProject.ext.versionCode}"
        buildConfigField "String", "VERSION_NAME", "\"${rootProject.ext.versionName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en', 'ru'
    }

    android.compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    android.kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}